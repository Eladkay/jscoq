/* build with:
 * npx @lezer/generator ui-js/mode/coq-mode.grammar -o ui-js/mode/coq-mode.grammar.js
 */

@top Document { Sentence* }

@skip { Space }

Sentence {
  element* endOfSentence | Bullet
}

Comment {
  commentStart (commentFragment|Comment)* commentEnd
}

element { Identifier | Keyword | Vernac | Tactic | Terminator | Admitter
          | String | Operator | Comment | anythingElse }

@external tokens bullet from "./coq-tokens" { Bullet }
@external tokens commentFragment from "./coq-tokens" { commentFragment }

@tokens {
  Identifier { $[a-zA-Z_0-9]+ }

  String { '"' (!["\\] | "\\" _)* '"' }

  Operator { "="+">" | ":=" | "<:" | "<<:" | ":>" | ("-"+">") | $[<>]"-"+">"? |
             "\\/" | "/\\" | ">=" | "<""="+ | "<>" | "++" | "::" | "||" | "&&" | ".."
  }

  commentStart { "(*" }
  commentEnd  { "*)" }

  Space { $[ \t\n\r]+ }

  anythingElse { "(" | ")" | ![(). \t\n\r"] }

  @precedence { commentStart, Identifier, Operator, anythingElse }
}

@external tokens endOfSentence from "./coq-tokens" { endOfSentence }
@external specialize { Identifier } specializeTactic from "./coq-tokens"
    { Tactic, Terminator, Admitter }
@external extend { Identifier } specializeKeyword from "./coq-tokens"
    { Keyword, Vernac }

@context sentenceContext from "./coq-tokens"

@detectDelim
